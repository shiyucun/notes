# 检测外部库和程序

- [检测外部库和程序](#检测外部库和程序)
  - [检测 python 解释器](#检测-python-解释器)
  - [检测 python](#检测-python)
  - [检测 python 模块和包](#检测-python-模块和包)

---

## 检测 python 解释器

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-011 LANGUAGES NONE)

find_package(PythonInterp 3.7 REQUIRED)
# 3.7 表示最低版本号
# REQUIRED 表示强制满足依赖关系，如果没找到合适的python解释器将中止执行

message(STATUS "PYTHON_VERSION: ${PYTHON_VERSION_STRING}")
# PYTHONINTERP_FOUND：是否找到解释器
# PYTHON_EXECUTABLE：Python解释器到可执行文件的路径
# PYTHON_VERSION_STRING：Python解释器的完整版本信息
# PYTHON_VERSION_MAJOR：Python解释器的主要版本号
# PYTHON_VERSION_MINOR ：Python解释器的次要版本号
# PYTHON_VERSION_PATCH：Python解释器的补丁版本号

execute_process(  # execute_process用于执行命令
    COMMAND
        ${PYTHON_EXECUTABLE} "-c" "print('Hello, world!')"
    RESULT_VARIABLE _status
    OUTPUT_VARIABLE _hello_world
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE
)

message(STATUS "RESULT_VARIABLE is: ${_status}")
message(STATUS "OUTPUT_VARIABLE is: ${_hello_world}")  # 使用message打印变量

include(CMakePrintHelpers)
cmake_print_variables(_status _hello_world) # 使用CMakePrintHelpers中cmake_print_variables打印多个变量
                                            # 输出：-- _status="0" ; _hello_world="Hello, world!"
```

find\_package 用于发现和设置cmake模块，这些模块包含cmake命令，用于标识系统标准位置中的包。

cmake模块文件名称为 Find\<name\>.cmake ，当调用 find\_package 时模块中的命令将会运行。

模块中的命令运行时可能会附带设置一些cmake变量。如上面代码中python相关的一些变量。

软件包没有安装在标准位置时，CMake无法正确定位它们。用户可以使用CLI的-D参数传递相应的选项，告诉CMake查看特定的位置。

Python解释器可以使用以下配置:

```shell
cmake -D PYTHON_EXECUTABLE=/custom/location/python ..
```

`cmake --help-module-list`可以获得现有模块列表；`cmake --help-module FindPythonInterp`可以获得对应模块的文档。

---

## 检测 python

创建 embedded_python.c，输入以下代码：

```c
#include <Python.h>

int main(int argc, char *argv[]) {
    Py_SetProgramName(argv[0]);

    Py_Initialize();

    PyRun_SimpleString("print('Hello, world.')");

    Py_Finalize();

    return 0;
}
```

创建CMakeLists.txt，输入以下内容：

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-012 LANGUAGES C)

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_EXTENSIONS OFF)
set(CMAKE_C_STANDARD_REQUIRED ON)

# 查找python解释器
find_package(PythonInterp REQUIRED)

# 查找python头文件和库模块
find_package(PythonLibs ${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR} EXACT REQUIRED)

add_executable(embedded_python)
target_sources(embedded_python
    PRIVATE
        embedded_python.c
)
target_include_directories(embedded_python
    PRIVATE
        ${PYTHON_INCLUDE_DIRS}
)
target_link_libraries(embedded_python
    PRIVATE
        ${PYTHON_LIBRARIES}
)
```

以上是将Python解释器嵌入到C程序中，通过 find_package 找到 PythonLibs ，还使用了 EXACT 关键字来限制指定的版本。

Python没有安装在标准目录时，可以通过CLI的-D选项传递 PYTHON_LIBRARY 和 PYTHON_INCLUDE_DIR 选项来强制CMake查找特定的目录。

- `PYTHON_LIBRARY`：指向Python库的路径。
- `PYTHON_INCLUDE_DIR`：Python.h所在的路径。

对于安装在非标准位置的Python，解释器及其开发的组件匹配为完全相同的版本可能很困难。

在 CMake 3.12 版本中增加了新的Python检测模块，旨在解决这个棘手的问题。CMake可以修改成如下内容：

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-012 LANGUAGES C)

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_EXTENSIONS OFF)
set(CMAKE_C_STANDARD_REQUIRED ON)

find_package(Python3 COMPONENTS Interpreter Development REQUIRED)

add_executable(embedded_python)
target_sources(embedded_python
    PRIVATE
        embedded_python.c
)
target_include_directories(embedded_python
    PRIVATE
        ${Python3_INCLUDE_DIRS}
)
target_link_libraries(embedded_python
    PRIVATE
        ${Python3_LIBRARIES}
)
```

---

## 检测 python 模块和包

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-013 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(PythonInterp REQUIRED)
find_package(PythonLibs ${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR} EXACT REQUIRED)

# 探测指定安装位置numpy安装状态
execute_process(
  COMMAND
      ${PYTHON_EXECUTABLE} "-c" "import re, numpy; print(re.compile('/__init__.py.*').sub('',numpy.__file__))"
  RESULT_VARIABLE _numpy_status
  OUTPUT_VARIABLE _numpy_location
  ERROR_QUIET
  OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  
if(NOT _numpy_status)
    set(NumPy ${_numpy_location} CACHE STRING "Location of NumPy")
endif()

# 获取numpy版本
execute_process(
  COMMAND
      ${PYTHON_EXECUTABLE} "-c" "import numpy; print(numpy.__version__)"
  OUTPUT_VARIABLE _numpy_version
  ERROR_QUIET
  OUTPUT_STRIP_TRAILING_WHITESPACE
  )


include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(NumPy
  FOUND_VAR NumPy_FOUND
  REQUIRED_VARS NumPy
  VERSION_VAR _numpy_version
  )
```

`execute_process`将作为通过子进程执行一个或多个命令。

最后，子进程返回值将保存到变量作为参数，传递给`RESULT_VARIABLE`，而管道标准输出和标准错误的内容将被保存到变量作为参数传递给`OUTPUT_VARIABLE`和`ERROR_VARIABLE`。

`execute_process`可以执行任何操作，并使用它们的结果来推断系统配置。

`find_package_handle_standard_args`提供了，用于处理与查找相关程序和库的标准工具。引用此命令时，可以正确的处理与版本相关的选项(REQUIRED和EXACT)，而无需更多的CMake代码。
