
# 从可执行文件到库

[cmake-cookbook](https://www.bookstack.cn/read/CMake-Cookbook/README.md)

- [从可执行文件到库](#从可执行文件到库)
  - [单个文件编译成可执行文件](#单个文件编译成可执行文件)
  - [切换生成器](#切换生成器)
  - [构建和链接静态库和动态库](#构建和链接静态库和动态库)
  - [用条件语句控制编译](#用条件语句控制编译)
  - [向用户显示选项](#向用户显示选项)
  - [指定编译器](#指定编译器)
  - [切换构建类型](#切换构建类型)
  - [设置编译器选项](#设置编译器选项)
  - [为语言设定标准](#为语言设定标准)
  - [使用控制流](#使用控制流)
  - [PUBLIC / PRIVATE / INTERFACE](#public--private--interface)

---

## 单个文件编译成可执行文件

执行以下命令创建文件：

```shell
mkdir 001 && cd 001
touch CMakeLists.txt
touch hello-world.cpp
```

在 CMakeLists.txt 文件中输入：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)  # cmake 最低版本要求，不满足时报 fatal 级别的错误
project (example-001 LANGUAGES CXX)  # 项目名称 和 显式声明项目语言(C、CXX、Fortran)

add_executable (hello-world hello-world.cpp)
# add_executable 用于添加可执行程序
# hello-world 是编译出的可执行程序的名字
# hello-world.cpp 编译和链接的源文件
```

在 hello-world.cpp 中输入：

```c++
#include <iostream>
#include <string>

std::string say_hello() {
    return std::string("Hello, world!");
}

int main() {
    std::cout << say_hello() << std::endl;
    return 0;
}
```

执行以下命令编译执行：

```shell
mkdir -p build && cd build && cmake .. # 配置项目
cmake --build . # 配置完成后在 build 目录中编译
./hello-world # 执行可执行程序
```

执行以下命令可以实现上述配置项目同样的效果：

```shell
cmake -H. -Bbuild
# -H  表示当前目录中搜索根 CMakeLists.txt 文件
# -B  告诉 CMake 在一个名为 build 的目录中生成所有的文件
```

`cmake --build . --target help`的 target 选项:

```text
The following are some of the valid targets for this Makefile:
... all (the default if no target is provided)
... clean
... depend
... rebuild_cache
... edit_cache # 允许直接编辑 cache
... hello-world
... hello-world.o
... hello-world.i
... hello-world.s
```

注：对于更复杂的项目，可能还会有 test、install、package 等选项。

---

## 切换生成器

执行`cmake --help`查看帮助信息：

```text
Usage

  cmake [options] <path-to-source>
  cmake [options] <path-to-existing-build>
  cmake [options] -S <path-to-source> -B <path-to-build>

Specify a source directory to (re-)generate a build system for it in the
current working directory.  Specify an existing build directory to
re-generate its build system.

Options
  -S <path-to-source>          = Explicitly specify a source directory.
  -B <path-to-build>           = Explicitly specify a build directory.
  -C <initial-cache>           = Pre-load a script to populate the cache.
  -D <var>[:<type>]=<value>    = Create or update a cmake cache entry.
  -U <globbing_expr>           = Remove matching entries from CMake cache.
  -G <generator-name>          = Specify a build system generator.
  -T <toolset-name>            = Specify toolset name if supported by
                                 generator.
  -A <platform-name>           = Specify platform name if supported by
                                 generator.
  -Wdev                        = Enable developer warnings.
  -Wno-dev                     = Suppress developer warnings.
  -Werror=dev                  = Make developer warnings errors.
  -Wno-error=dev               = Make developer warnings not errors.
  -Wdeprecated                 = Enable deprecation warnings.
  -Wno-deprecated              = Suppress deprecation warnings.
  -Werror=deprecated           = Make deprecated macro and function warnings
                                 errors.
  -Wno-error=deprecated        = Make deprecated macro and function warnings
                                 not errors.
  -E                           = CMake command mode.
  -L[A][H]                     = List non-advanced cached variables.
  --build <dir>                = Build a CMake-generated project binary tree.
  --install <dir>              = Install a CMake-generated project binary
                                 tree.
  --open <dir>                 = Open generated project in the associated
                                 application.
  -N                           = View mode only.
  -P <file>                    = Process script mode.
  --find-package               = Run in pkg-config like mode.
  --graphviz=[file]            = Generate graphviz of dependencies, see
                                 CMakeGraphVizOptions.cmake for more.
  --system-information [file]  = Dump information about this system.
  --log-level=<ERROR|WARNING|NOTICE|STATUS|VERBOSE|DEBUG|TRACE>
                               = Set the verbosity of messages from CMake
                                 files.  --loglevel is also accepted for
                                 backward compatibility reasons.
  --debug-trycompile           = Do not delete the try_compile build tree.
                                 Only useful on one try_compile at a time.
  --debug-output               = Put cmake in a debug mode.
  --trace                      = Put cmake in trace mode.
  --trace-expand               = Put cmake in trace mode with variable
                                 expansion.
  --trace-source=<file>        = Trace only this CMake file/module.  Multiple
                                 options allowed.
  --trace-redirect=<file>      = Redirect trace output to a file instead of
                                 stderr.
  --warn-uninitialized         = Warn about uninitialized values.
  --warn-unused-vars           = Warn about unused variables.
  --no-warn-unused-cli         = Don't warn about command line options.
  --check-system-vars          = Find problems with variable usage in system
                                 files.
  --help,-help,-usage,-h,-H,/? = Print usage information and exit.
  --version,-version,/V [<f>]  = Print version number and exit.
  --help-full [<f>]            = Print all help manuals and exit.
  --help-manual <man> [<f>]    = Print one help manual and exit.
  --help-manual-list [<f>]     = List help manuals available and exit.
  --help-command <cmd> [<f>]   = Print help for one command and exit.
  --help-command-list [<f>]    = List commands with help available and exit.
  --help-commands [<f>]        = Print cmake-commands manual and exit.
  --help-module <mod> [<f>]    = Print help for one module and exit.
  --help-module-list [<f>]     = List modules with help available and exit.
  --help-modules [<f>]         = Print cmake-modules manual and exit.
  --help-policy <cmp> [<f>]    = Print help for one policy and exit.
  --help-policy-list [<f>]     = List policies with help available and exit.
  --help-policies [<f>]        = Print cmake-policies manual and exit.
  --help-property <prop> [<f>] = Print help for one property and exit.
  --help-property-list [<f>]   = List properties with help available and
                                 exit.
  --help-properties [<f>]      = Print cmake-properties manual and exit.
  --help-variable var [<f>]    = Print help for one variable and exit.
  --help-variable-list [<f>]   = List variables with help available and exit.
  --help-variables [<f>]       = Print cmake-variables manual and exit.
  
Generators

The following generators are available on this platform (* marks default):
* Unix Makefiles               = Generates standard UNIX makefiles.
  Green Hills MULTI            = Generates Green Hills MULTI files
                                 (experimental, work-in-progress).
  Ninja                        = Generates build.ninja files.
  Watcom WMake                 = Generates Watcom WMake makefiles.
  CodeBlocks - Ninja           = Generates CodeBlocks project files.
  CodeBlocks - Unix Makefiles  = Generates CodeBlocks project files.
  CodeLite - Ninja             = Generates CodeLite project files.
  CodeLite - Unix Makefiles    = Generates CodeLite project files.
  Sublime Text 2 - Ninja       = Generates Sublime Text 2 project files.
  Sublime Text 2 - Unix Makefiles
                               = Generates Sublime Text 2 project files.
  Kate - Ninja                 = Generates Kate project files.
  Kate - Unix Makefiles        = Generates Kate project files.
  Eclipse CDT4 - Ninja         = Generates Eclipse CDT 4.0 project files.
  Eclipse CDT4 - Unix Makefiles= Generates Eclipse CDT 4.0 project files.
```

使用 example-001 示例，`cmake ..`修改为`cmake -G Ninja ..`，重新配置项目，观察结果。

注：

1. `cmake -H. -Bbuild`修改为`cmake -G Ninja -H. -Bbuild`可以达到同样的效果。
2. 没有安装`ninja`时，在`ubuntu`中可以使用`sudo apt install ninja-build`进行安装。

---

## 构建和链接静态库和动态库

执行以下命令创建文件：

```shell
mkdir 002 && cd 002
touch CMakeLists.txt
touch hello-world.cc
touch Message.cc
touch Message.hpp
```

在 CMakeLists.txt 文件中输入：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-002 LANGUAGES CXX)

# 使用源文件生成静态库
add_library (message
  STATIC
    Message.hpp
    Message.cc
  )

# add_library 说明：
#   将指定的源文件生成指定类型的库
#   参数一：库名称(编译后生成的库文件以 lib 开头，不同类型的库以不同扩展名结尾)
#   参数二：库类型
#   参数二的有效值：
#     STATIC：创建静态库
#     SHARED：创建动态库
#     OBJECT：创建对象库
#     MODULE：DSO组，用于构建运行时插件
#     IMPORTED：此类库目标表示位于项目外部的库。用于对现有依赖项进行构建，因此 IMPORTED 库被视为不可变的
#     INTERFACE：与IMPORTED库类似。不过，该类型库可变，没有位置信息。用于项目之外的目标构建使用
#     ALIAS：这种库为项目中已存在的库目标定义别名(不能为IMPORTED库定义别名)

add_executable (hello-world hello-world.cc)

# 可执行程序链接静态库
target_link_libraries (hello-world message)
```

在 Message.hpp 文件中输入：

```c++
#pragma once

#include <iosfwd>
#include <string>

class Message {
  public:
    Message(const std::string& m)
        : message_(m) {
    }

    friend std::ostream& operator<<(std::ostream& os, Message& obj) {
        return obj.printObject(os);
    }

  private:
    std::string message_;
    std::ostream& printObject(std::ostream& os);
};
```

在 Message.cc 文件中输入：

```c++
#include "Message.hpp"

#include <iostream>
#include <string>

std::ostream& Message::printObject(std::ostream& os) {
    os << "This is my very nice message: " << std::endl;
    os << message_;

    return os;
}
```

在 hello-world.cc 文件中输入：

```c++
#include "Message.hpp"

#include <cstdlib>
#include <iostream>

int main() {
    Message say_hello("Hello, CMake World!");

    std::cout << say_hello << std::endl;

    Message say_goodbye("Goodbye, CMake World");

    std::cout << say_goodbye << std::endl;

    return EXIT_SUCCESS;
}
```

执行`cmake -H. -Bbuild && cd build && cmake --build .`并查看输出：

```text
-- The CXX compiler identification is GNU 9.3.0
-- Check for working CXX compiler: /usr/bin/c++
-- Check for working CXX compiler: /usr/bin/c++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Detecting CXX compile features
-- Detecting CXX compile features - done
-- Configuring done
-- Generating done
-- Build files have been written to: /home/shiyucun/workspace/code/learn/cmake/002/build
Scanning dependencies of target message
[ 25%] Building CXX object CMakeFiles/message.dir/Message.cc.o
[ 50%] Linking CXX static library libmessage.a
[ 50%] Built target message
Scanning dependencies of target hello-world
[ 75%] Building CXX object CMakeFiles/hello-world.dir/hello-world.cc.o
[100%] Linking CXX executable hello-world
```

关于 OBJECT 对象库的使用，修改 CMakeLists.txt 如下：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-002 LANGUAGES CXX)

add_library (message-objs
  OBJECT
    Message.hpp
    Message.cc
  )

set_target_properties (message-objs
  PROPERTIES
    POSITION_INDEPENDENT_CODE 1  # 确保编译的目标文件与生成位置无关
  )


add_library (message-shared
  SHARED                            # 生成动态库
    $<TARGET_OBJECTS:message-objs>  # 对象库生成器表达式
  )
set_target_properties (message-shared
  PROPERTIES
    OUTPUT_NAME "message"           # 设置输出库名字为 libmessage.so
  )

add_library (message-static
  STATIC                            # 生成静态库
    $<TARGET_OBJECTS:message-objs>  # 对象库生成器表达式
  )
set_target_properties (message-static
  PROPERTIES
    OUTPUT_NAME "message"           # 设置输出库名字为 libmessage.a
  )

add_executable (hello-world hello-world.cc)

target_link_libraries (hello-world message-static)
```

---

## 用条件语句控制编译

使用 example-002 示例，修改 CMakeLists.txt ：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-002 LANGUAGES CXX)

set (USE_LIBRARY OFF)  # 定义变量 USE_LIBRARY，值设置为 OFF

message (STATUS "Compile sources into a library? ${USE_LIBRARY}")  # 输出提示信息和变量值

set (BUILD_SHARED_LIBS OFF)  # cmake 全局变量 BUILD_SHARED_LIBS，控制是否生成动态库，值设置为 OFF

list (APPEND _sources Message.hpp Message.cc) # 定义列表，并将源文件输入到列表
# 列表子命令：
# LENGTH: 获取列表长度
#    list (LENGTH <list> <output variable>)
#        <output variable>为新创建的变量，用于存储列表的长度
# GET: 获取指定索引的元素，0为第一个元素，-1为最后一个元素，可反向索引
#    list (GET <list> <element index> [<element index> ...] <output variable>)
#        <output variable>为新创建的变量，存储指定索引元素的返回结果，也是一个列表
# JOIN: 将列表中的元素用连接字符串连接起来组成一个字符串
#    list (JOIN <list> <glue> <output variable>)
#        将列表中的元素用<glue>链接起来，组成一个字符串后，返回给<output variable>变量
#        对于不属于列表的多个字符串的连接操作，可以使用string()命令的连接操作
# SUBLIST: 用于获取子列表
#    list (SUBLIST <list> <begin> <length> <output variable>)
#        返回列表<list>中，从索引<begin>开始，长度为<length>的子列表
#        如果长度<length>为0，返回的是空列表
#        如果长度<length>为-1或列表的长度小于<begin>+<length>，那么将列表中从<begin>索引开始的剩余元素返回
# FIND: 用于查找列表是否存在指定的元素
#    list (FIND <list> <value> <output variable>)
#        如果列表<list>中存在<value>，那么返回<value>在列表中的索引，如果未找到则返回-1
# APPEND: 将元素追加到列表
#    list (APPEND <list> [<element> ...])
#        此命令会改变原列表的值
# INSERT: 在指定位置将元素插入到列表
#    list (INSERT <list> <element_index> <element> [<element> ...])
#        <element_index>为列表指定的位置，如果元素的位置超出列表的范围，会报错。此命令会改变原来列表的值
# POP_BACK: 将列表中最后元素移除
#    list (POP_BACK <list> [<out-var>...])
#        如果未指定输出变量，则仅仅是将原列表的最后一个元素移除
#        如果指定了输出变量，则会将最后一个元素移入到该变量，并将元素从原列表中移除
#        如果指定了多个输出变量，则依次将原列的最后一个元素移入到输出变量中
#        如果输出变量个数大于列表的长度，那么超出部分的输出变量未定义
# POP_FRONT: 将列表中第一个元素移除
#    list (POP_FRONT <list> [<out-var>...])
#        如果未指定输出变量，则仅仅是将原列表的第一个元素移除
#        如果指定了输出变量，则会将第一个元素移入到该变量，并将元素从原列表中移除
#        如果指定了多个输出变量，则依次将原列的第一个元素移入到输出变量中
#        如果输出变量个数大于列表的长度，那么超出部分的输出变量未定义
# PREPEND: 将元素插入到列表的0索引位置
#    list (PREPEND <list> [<element> ...])
#        如果待插入的元素是多个，则相当于把多个元素整体“平移”到原列表0索引的位置
# REMOVE_ITEM: 将指定的元素从列表中移除
#    list (REMOVE_ITEM <list> <value> [<value> ...])
#        注意：指定的是元素的值，当指定的值在列表中存在重复的时候，会删除所有重复的值
# REMOVE_AT: 将指定索引的元素从列表中移除
#    list (REMOVE_AT <list> <index> [<index> ...])
#        注意：指定的是元素的索引，当指定的索引不存在的时候，会提示错误；如果指定的索引存在重复，则只会执行一次删除动作
# REMOVE_DUPLICATES: 移除列表中的重复元素
#    list (REMOVE_DUPLICATES <list>)
#        如果没有重复元素，原列表不会做更改
#        如果原列表所有元素都是一样的，则会保留一个
#        如果有多个重复元素，则保留第一个
# ......

if (USE_LIBRARY)
  add_library (message ${_sources})

  add_executable (hello-world hello-world.cc)
  target_link_libraries (hello-world message)
else()
  add_executable (hello-world hello-world.cc ${_sources})
endif()
```

执行`cmake -H. -Bbuild && cd build && cmake --build .`并观察输出：

```text
-- The CXX compiler identification is GNU 9.3.0
-- Check for working CXX compiler: /usr/bin/c++
-- Check for working CXX compiler: /usr/bin/c++ -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Detecting CXX compile features
-- Detecting CXX compile features - done
-- Compile sources into a library? OFF
-- Configuring done
-- Generating done
-- Build files have been written to: /home/shiyucun/workspace/code/learn/cmake/002/build
Scanning dependencies of target hello-world
[ 33%] Building CXX object CMakeFiles/hello-world.dir/hello-world.cc.o
[ 66%] Building CXX object CMakeFiles/hello-world.dir/Message.cc.o
[100%] Linking CXX executable hello-world
[100%] Built target hello-world
```

扩展：

逻辑真或假可以用多种方式表示。

- 逻辑真：1、ON、YES、true、Y或非零数。
- 逻辑假：0、OFF、NO、false、N、IGNORE、NOTFOUND、空字符串，或者以-NOTFOUND为后缀。

CMake内部要查询 BUILD_SHARED_LIBS 全局变量，所以 add_library 命令可以在不传递 STATIC/SHARED/OBJECT 参数的情况下调用。

如果不传递这些参数 且 BUILD_SHARED_LIBS 为 false 或 未定义，则将生成一个静态库。

---

## 向用户显示选项

使用 example-002 示例，修改 CMakeLists.txt：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-002 LANGUAGES CXX)

option (USE_LIBRARY "Compile sources into a library" OFF)  # 定义选项，默认值为 OFF，可以使用 cmake -D 的 CLI 选项修改
# -D 开关用于为 CMake 设置任何类型的变量：逻辑变量、路径等等

message (STATUS "Compile sources into a library? ${USE_LIBRARY}")

set (BUILD_SHARED_LIBS OFF)

list (APPEND _sources Message.hpp Message.cc)

if (USE_LIBRARY)
  add_library (message ${_sources})

  add_executable (hello-world hello-world.cc)
  target_link_libraries (hello-world message)
else()
  add_executable (hello-world hello-world.cc ${_sources})
endif()
```

option可接受三个参数：

option(<option_variable> "help string" [initial value])

- option_variable 表示该选项的变量的名称
- help string 记录选项的字符串，在 CMake 的终端或图形用户界面中可见
- initial value 选项的默认值，可以是 ON 或 OFF，未指定时默认为 false

cmake_dependent_option:

```cmake
# cmake_dependent_option 是 cmake 内置的一个 module，用于生成 option，生成的 option 依赖其它 option
# 接受三个参数：
#    1. 生成的 option 的名字
#    2. 描述字符串
#    3. 三元判断式，如 OFF "USE_LIBRARY" ON，当 USE_LIBRARY 为真时取 OFF，否则取值 ON

include(CMakeDependentOption)

cmake_dependent_option (MAKE_STATIC_LIBRARY
  "Compile sources into a static library"
  OFF "USE_LIBRARY" ON
  )
```

CMake有适当的机制，通过包含模块来扩展其语法和功能，这些模块要么是CMake自带的，要么是定制的。

在 cmake 手册中的任何模块都可以通过命令行的方式查看 modules 帮助信息：

- 执行`cmake --help-modules`可以查看所有模块的帮助信息
- 执行`cmake --help-module xxx`可以查看指定模块的帮助信息

用户选项可以通过以下命令行列出：

```shell
cmake -L cmake_file_path    # 列出所有选项，不包括advance选项
cmake -LA cmake_file_path    # 列出所有选项，包括advance选项
cmake -LAH cmake_file_path    # 列出所有选项，包括advance选项，包括选项的注释/解释
cmake -LH cmake_file_path    # 列出所有选项，不包括advance选项，包括选项的注释/解释
```

---

## 指定编译器

CMake将语言的编译器存储在`CMAKE_<LANG>_COMPILER`变量中，其中`<LANG>`是受支持的任何一种语言，其值可以是 CC(c编译器)、CXX(c++编译器)、FC(Fortran编译器)。可以通过以下方式修改编译器：

```shell
# 方式一：
cmake -DCMAKE_CXX_COMPILER=clang++ ..

# 方式二：
env CXX=clang++ cmake ..
```

配置时，CMake会进行一系列平台测试，以确定哪些编译器可用，以及它们是否适合当前的项目。

一个合适的编译器不仅取决于我们所使用的平台，还取决于我们想要使用的生成器。

CMake执行的第一个测试是基于项目语言的编译器的名称。

例如：

- cc 是一个工作的 C 编译器，那么它 将用作 C项目的 默认编译器。
- GNU/linux上，使用 Unix  Makefile 或 Ninja 时，GCC家族中的 编译器很可能是C++、C 和 Fortran的默认选择。
- Microsoft Windows上，将选择 Visual Studio 中的 C++ 和 C 编译器(前提是Visual Studio是生成器)。
- Microsoft Windows上，如果选择 MinGW 或 MSYS  Makefile 作为生成器，则默认使用 MinGW 编译器。

执行 `cmake --system-information information.txt` 可以获取更多信息，信息中心可以看到很多默认值以及默认标志。

CMake提供了额外的变量来与编译器交互：

- CMAKE\_\<LANG\>\_COMPILER\_LOADED: 如果为项目启用了语言\<LANG\>，则将设置为TRUE。
- CMAKE\_\<LANG\>\_COMPILER\_ID: 编译器标识字符串，编译器供应商所特有。例如，GCC用于GNU编译器集合，AppleClang用于macOS上的Clang, MSVC用于Microsoft Visual Studio编译器。注意，不能保证为所有编译器或语言定义此变量。
- CMAKE\_COMPILER\_IS\_GNU\<LANG\>: 如果语言\<LANG\>是GNU编译器集合的一部分，则将此逻辑变量设置为TRUE。注意变量名的\<LANG\>部分遵循GNU约定：C语言为CC, C++语言为CXX, Fortran语言为G77。
- CMAKE\_\<LANG\>\_COMPILER\_VERSION: 此变量包含一个字符串，该字符串为给定语言的编译器版本。版本信息在`major[.minor[.patch[.tweak]]]`中给出。但是，对于CMAKE\_\<LANG\>\_COMPILER\_ID，不能保证所有编译器或语言都定义了此变量。

如下：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-003 LANGUAGES C CXX)

message (STATUS "Is the C++ compiler loaded? ${CMAKE_CXX_COMPILER_LOADED}")

if (CMAKE_CXX_COMPILER_LOADED)
    message (STATUS "The C++ compiler ID is: ${CMAKE_CXX_COMPILER_ID}")
    message (STATUS "Is the C++ from GNU? ${CMAKE_COMPILER_IS_GNUCXX}")
    message (STATUS "The C++ compiler version is: ${CMAKE_CXX_COMPILER_VERSION}")
endif()

message (STATUS "Is the C compiler loaded? ${CMAKE_C_COMPILER_LOADED}")

if (CMAKE_C_COMPILER_LOADED)
    message (STATUS "The C compiler ID is: ${CMAKE_C_COMPILER_ID}")
    message (STATUS "Is the C from GNU? ${CMAKE_COMPILER_IS_GNUCC}")
    message (STATUS "The C compiler version is: ${CMAKE_C_COMPILER_VERSION}")
endif()
```

---

## 切换构建类型

CMake可以配置构建类型，例如：Debug、Release等。

配置时，可以为Debug或Release构建设置相关的选项或属性，例如：编译器和链接器标志。

控制生成构建系统使用的配置变量是 CMAKE_BUILD_TYPE。该变量默认为空，CMake识别的值为:

- Debug：用于在没有优化的情况下，使用带有调试符号构建库或可执行文件。
- Release：用于构建的优化的库或可执行文件，不包含调试符号。
- RelWithDebInfo：用于构建较少的优化库或可执行文件，包含调试符号。
- MinSizeRel：用于不增加目标代码大小的优化方式，来构建库或可执行文件。

如下：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-004 LANGUAGES C CXX)

if (NOT CMAKE_BUILD_TYPE)
    set (CMAKE_BUILD_TYPE Release CACHE STRING "Build type" FORCE)  # 设置了默认的构建类型，变量类型设置为缓存变量
endif()
message (STATUS "Build type: ${CMAKE_BUILD_TYPE}")

# 输出 CMake 设置的 C 语言相应编译标志
message (STATUS "C flags, Debug configuration: ${CMAKE_C_FLAGS_DEBUG}")
message (STATUS "C flags, Release configuration: ${CMAKE_C_FLAGS_RELEASE}")
message (STATUS "C flags, Release configuration with Debug info: ${CMAKE_C_FLAGS_RELWITHDEBINFO}")
message (STATUS "C flags, minimal Release configuration: ${CMAKE_C_FLAGS_MINSIZEREL}")

# 输出 CMake 设置的 C++ 语言相应编译标志
message (STATUS "C++ flags, Debug configuration: ${CMAKE_CXX_FLAGS_DEBUG}")
message (STATUS "C++ flags, Release configuration: ${CMAKE_CXX_FLAGS_RELEASE}")
message (STATUS "C++ flags, Release configuration with Debug info: ${CMAKE_CXX_FLAGS_RELWITHDEBINFO}")
message (STATUS "C++ flags, minimal Release configuration: ${CMAKE_CXX_FLAGS_MINSIZEREL}")
```

set 方法用于给下面的变量设置值：

- 一般变量
- 缓存变量
- 环境变量

一般变量：

```cmake
set(<variable> <value>... [PARENT_SCOPE])
# variable：变量，只能有一个
# value：可以有0个、1个或多个，当为0个时，方法同 unset，用于取消设置的值
# PARENT_SCOPE：父作用域
# 关于 PARENT_SCOPE：
#    原本父目录中变量的值会拷贝到子目录，在子目录中修改变量的值时，父目录中变量的值不会改变
#    当为一般变量加上 PARENT_SCOPE 修饰时，子目录中修改变量值时也会影响父目录中该变量的值
#    include()/macro() 其实是包含了一个其它文件，类似于 c/c++ 中的 #include，
#        使用了 include()/macro() 的 CMakeLists.txt 文件会将其它文件的代码直接拷贝到本文件中，
#        因此他们的作用域是一致的，即不加 PARENT_SCOPE 修饰也可以修改原变量的值
#    对于 function，与 add_subdirectory 一致，即是两个不同的作用域，因此需要加上 PARENT_SCOPE 修饰才会影响原变量的值
```

缓存变量：

```cmake
set(<variable> <value>... CACHE <type> <docstring> [FORCE])
# variable：只能有1个
# value：可以有0个、1个或多个，当为0个时，方法同 unset，用于取消设置的值
# CACHE：关键字，说明是缓存变量
# type：类型，必须为以下一种
#    BOOL：有ON/OFF，两种取值
#    FILEPATH：文件的全路径
#    PATH：目录路径
#    STRING：字符串
#    INTERNAL：字符串
# docstring：总结性文字(字符串)
# [FORCE]：变量名相同，第二次调用set方法时，第一次的value将会被覆盖
#
# 缓存变量相当于一个全局变量，我们在同一个 cmake 工程中都可以使用
# CMake 规定，有一个与 Cache 变量同名的 Normal 变量出现时，后面使用这个变量的值都是以 Normal 为准，
#        如果没有同名的 Normal 变量，CMake 才会自动使用 Cache 变量
# 所有的 Cache 变量都会出现在 CMakeCache.txt 文件中
# 修改 Cache 变量：
#    方法一：set(<variable> <value> CACHE INSTERNAL FORCE)
#    方法二：终端中使用 cmake -D var=value ..
```

环境变量：

```cmake
set(ENV{<variable>} [<value>])
# variable：只能有1个
# value：一般只有1个，为空时将清除之前设置的值，多个时取最近的一个，之后的值被忽略
```

---

## 设置编译器选项

CMake为调整或扩展编译器标志提供了很大的灵活性，有以下两种方法:

- CMake将编译选项视为目标属性。因此为每个目标设置编译选项，不覆盖CMake默认值
- 使用 -DCLI 标志直接修改 CMAKE\_\<LANG\>\_FLAGS\_\<CONFIG\> 变量，将影响项目中的所有目标，并覆盖或扩展CMake默认值

第一种方法，使用 example-002，修改 CMakeLists.txt 如下：

```cmake
cmake_minimum_required (VERSION 3.5 FATAL_ERROR)

project (example-002 LANGUAGES CXX)

message ("C++ compiler flags: ${CMAKE_CXX_FLAGS}")  # 打印当前编译器选项

list (APPEND flags "-fPIC" "-Wall")  # 追加编译选项
if (NOT WIN32)
  list (APPEND flags "-Wextra" "-Wpedantic")
endif()

add_library (message
  STATIC
    Message.cc
    Message.hpp
  )
target_compile_options (message  # 为目标库设置编译选项
  PRIVATE  # 编译选项的可见性
    ${flags}
  )

add_executable (hello-world hello-world.cc)
target_compile_options (hello-world  # 为可执行程序设置编译选项
  PRIVATE  # 编译选项的可见性
    "-fPIC"
  )
target_link_libraries (hello-world message)
```

第二种方法，不用对 CMakeLists.txt 进行修改，使用 CMake 参数进行配置：

```cmake
cmake -D CMAKE_CXX_FLAGS="-O3 -std=c++11" ..
```

推荐为每个目标设置编译器标志。

使用target_compile_options()不仅允许对编译选项进行细粒度控制，而且还可以更好地与CMake的更高级特性进行集成。

大多数时候，编译器有特性标示。如果项目需要跨平台，那么就要为每个编译器分别设置编译选项，有三种方法可以解决这个问题。

- 根据编译器ID值 CMAKE\_\<LANG\>\_COMPILER\_ID 设置cmake变量 CMAKE\_\<LANG\>\_FLAGS\_\<CONFIG\>
- 定义特定的标志列表
- 检查所需的标志集是否与给定的编译器一起工作，项目中实际上只使用有效标志

第一种方法：

```cmake
if (CMAKE_CXX_COMPILER_ID MATCHES GNU)
  list (APPEND CMAKE_CXX_FLAGS "-fno-rtti" "-fno-exceptions")
  list (APPEND CMAKE_CXX_FLAGS_DEBUG "-Wsuggest-final-types" "-Wsuggest-final-methods" "-Wsuggest-override")
  list (APPEND CMAKE_CXX_FLAGS_RELEASE "-O3" "-Wno-unused")
endif()
if (CMAKE_CXX_COMPILER_ID MATCHES Clang)
  list (APPEND CMAKE_CXX_FLAGS "-fno-rtti" "-fno-exceptions" "-Qunused-arguments" "-fcolor-diagnostics")
  list (APPEND CMAKE_CXX_FLAGS_DEBUG "-Wdocumentation")
  list (APPEND CMAKE_CXX_FLAGS_RELEASE "-O3" "-Wno-unused")
endif()
```

第二种方法：

```cmake
set (COMPILER_FLAGS)
set (COMPILER_FLAGS_DEBUG)
set (COMPILER_FLAGS_RELEASE)

if (CMAKE_CXX_COMPILER_ID MATCHES GNU)
  list (APPEND CXX_FLAGS "-fno-rtti" "-fno-exceptions")
  list (APPEND CXX_FLAGS_DEBUG "-Wsuggest-final-types" "-Wsuggest-final-methods" "-Wsuggest-override")
  list (APPEND CXX_FLAGS_RELEASE "-O3" "-Wno-unused")
endif()
if (CMAKE_CXX_COMPILER_ID MATCHES Clang)
  list (APPEND CXX_FLAGS "-fno-rtti" "-fno-exceptions" "-Qunused-arguments" "-fcolor-diagnostics")
  list (APPEND CXX_FLAGS_DEBUG "-Wdocumentation")
  list (APPEND CXX_FLAGS_RELEASE "-O3" "-Wno-unused")
endif()

target_compile_options (target  # 在使用生成器表达式来设置编译器标志的基础上，为每个配置和每个目标生成构建系统
  PRIVATE
    ${CXX_FLAGS}
    "$<$<CONFIG:Debug>:${CXX_FLAGS_DEBUG}>"
    "$<$<CONFIG:Release>:${CXX_FLAGS_RELEASE}>"
  )
```

第三种方法是使用 check-and-set 检查每一个编译选项是否能够与给定编译器一起工作。

第一种和第二种方法都有优缺点，第三种方法更为健壮，推荐使用第三种方法。

---

## 为语言设定标准

设定语言标准可以在编译选项中设置，也可以使用 set_target_properties 设置，如下：

```cmake
set_target_properties (target
  PROPERTIES
    CXX_STANDARD 14              # C++ 标准
    CXX_EXTENSIONS OFF           # 是否启用 C++ 编译器扩展
    CXX_STANDARD_REQUIRED ON     # CXX_STANDARD 是否必须使用指定的 C++ 标准，设置 OFF 时将会查找合适的 C++标准
    POSITION_INDEPENDENT_CODE 1  # 生成位置无关代码 -fPIC
)
```

---

## 使用控制流

```cmake
list (APPEND sources a.cc b.cc)

foreach (_source IN LISTS sources)
  set_source_files_properties (${_source} PROPERTIES COMPILE_FLAGS -O2)  # 设置 sources 中文件编译优化级别
endforeach()

foreach (_source ${sources})
  get_source_file_property (_flags ${_source} COMPILE_FLAGS)  # 获取每个文件的编译选项
endforeach()
```

foreach()的四种使用方式：

- foreach (loop_var arg1 arg2 ...)：其中提供循环变量和显式项列表。注意：如果项目列表位于变量中，则必须显式展开它，也就是必须使用 ${sources} 这种方式。
- 通过指定一个范围，可以对整数进行循环，如：foreach (loop_var range total) 或 foreach(loop_var range start stop [step])
- 对列表值变量的循环，如：foreach (loop_var IN LISTS [list1[...]])，参数解释为列表，其内容就会自动展开
- 对变量的循环，如：foreach(loop_var IN ITEMS [item1 [...]])，参数的内容没有展开

---

## PUBLIC / PRIVATE / INTERFACE

target_include_directories()：指定目标包含的头文件路径。

target_link_libraries()：指定目标链接的库。

target_compile_options()：指定目标的编译选项。

目标由 add_library() 或 add_executable() 生成。

这三个指令类似，这里以 target_include_directories() 为例进行讲解。目录如下：

```text
cmake-test/                 # 工程主目录，main.c 调用 libhello-world.so
├── CMakeLists.txt
├── hello-world             # 生成 libhello-world.so，调用 libhello.so 和 libworld.so
│   ├── CMakeLists.txt
│   ├── hello               # 生成 libhello.so 
│   │   ├── CMakeLists.txt
│   │   ├── hello.c
│   │   └── hello.h         # libhello.so 对外的头文件
│   ├── hello_world.c
│   ├── hello_world.h       # libhello-world.so 对外的头文件
│   └── world               # 生成 libworld.so
│       ├── CMakeLists.txt
│       ├── world.c
│       └── world.h         # libworld.so 对外的头文件
└── main.c
```

关键字用法说明：

`PRIVATE`：私有的。

- 生成 libhello-world.so 时，只在 hello_world.c 中包含了 hello.h。
- libhello-world.so 对外的头文件 —— hello_world.h 中不包含 hello.h。
- main.c 不会调用 hello.c 中的函数，或者说 main.c 不知道 hello.c 的存在。

以上三个条件成立，则在 hello-world/CMakeLists.txt 中应该写入：

```cmake
target_link_libraries(hello-world PRIVATE hello)
target_include_directories(hello-world PRIVATE hello)
```

`INTERFACE`：接口。

- 生成 libhello-world.so 时，只在 libhello-world.so 对外的头文件 hello_world.h 中包含了 hello.h。
- hello_world.c 中不包含 hello.h，即 libhello-world.so 不使用 libhello.so 提供的功能，只使用 hello.h 中的某些信息，比如结构体。
- main.c 需要使用 libhello.so 中的功能。

以上三个条件成立，则在 hello-world/CMakeLists.txt 中应该写入：

```cmake
target_link_libraries(hello-world INTERFACE hello)
target_include_directories(hello-world INTERFACE hello)
```

`PUBLIC`：公开的。`PUBLIC = PRIVATE + INTERFACE`。

- 生成 libhello-world.so 时，在 hello_world.c 和 hello_world.h 中都包含了 hello.h。
- main.c 中也需要使用 libhello.so 提供的功能。

以上两个条件成立，则在 hello-world/CMakeLists.txt 中应该写入：

```cmake
target_link_libraries(hello-world PUBLIC hello)
target_include_directories(hello-world PUBLIC hello)
```

实际上，这三个关键字指定的是目标文件依赖项的使用范围（scope）或者一种传递（propagate）。

可执行文件依赖 libhello-world.so， libhello-world.so 依赖 libhello.so 和 libworld.so。

main.c 不使用 libhello.so 的任何功能，因此 libhello-world.so 不需要将其依赖 libhello.so 传递给 main.c，
因此 hello-world/CMakeLists.txt 中使用 PRIVATE 关键字。

main.c 使用 libhello.so 的功能，但是 libhello-world.so 不使用，因此 hello-world/CMakeLists.txt 中使用 INTERFACE 关键字。

main.c 和 libhello-world.so 都使用 libhello.so 的功能，因此，hello-world/CMakeLists.txt 中使用 PUBLIC 关键字。
