
# 创建和运行测试

- [创建和运行测试](#创建和运行测试)
  - [CMake集成第三方库](#cmake集成第三方库)
    - [第三方库使用 CMake](#第三方库使用-cmake)
    - [第三方库未使用 CMake](#第三方库未使用-cmake)
    - [第三方库使用 CMake，在项目外部构建](#第三方库使用-cmake在项目外部构建)
    - [第三方库未使用 CMake，在项目外部构建](#第三方库未使用-cmake在项目外部构建)
  - [创建下游友好的 CMake 项目](#创建下游友好的-cmake-项目)
  - [find\_package 基本调用形式和模块模式](#find_package-基本调用形式和模块模式)
  - [find\_package 完整形式](#find_package-完整形式)
  - [find\_package 版本选择](#find_package-版本选择)
  - [find\_package 搜索过程](#find_package-搜索过程)
  - [find\_package 包文件接口变量](#find_package-包文件接口变量)
  - [使用GoogleTest库进行单元测试](#使用googletest库进行单元测试)
  - [使用动态分析来检测内存缺陷](#使用动态分析来检测内存缺陷)

---

## CMake集成第三方库

在 C/C++ 项目中使用第三方库有两种方式：

1. 第三方库在项目外部单独构建：从官网或系统包管理程序上下载预编译好的包，或者事先在项目外部的其他路径下使用库的源码进行编译。
2. 第三方库的构建集成到项目的构建过程里，从源码开始编译。

第一种方式并不是所有的平台/发行版/系统版本都能轻松完成各种库的编译和安装。但这种方式很适合编译时间久或者工具链复杂的第三方库。

第二种方式对开发者比较友好，简单粗暴的实现方式是使用 "git submodule" 拉取依赖源码，或者编写一些脚本管理第三方库。
如果是使用 CMake 作为构建系统的项目，可以利用 FetchContent 模块来管理依赖。

FetchCotent 主要有以下几个优点：

- 支持 git clone、下载源码压缩包等多种方式获取代码
- 可以处理依赖树中存在的重复依赖
- 在 configure 阶段拉取代码，build 阶段编译代码，符合 CMake 原有机制，减少了执行多个命令的麻烦
- 用 CMake 一套工具控制一切编译、安装任务

上面提到了两种使用第三方库的方式，在 CMake 项目中还可以分出两种子情况，即第三方库是否也使用 CMake 作为构建系统，下面就介绍如何处理这四种情况。

---

### 第三方库使用 CMake

这种情况可以使用FetchContent模块获取第三方库的源码。

核心函数只有两个：FetchContent_Declare 和 FetchContent_Populate。

前者用于声明信息，后者用于下载代码。

```cmake
include(FetchContent)  # 引入 FetchContent

set(FETCHCONTENT_BASE_DIR ${CMAKE_SOURCE_DIR}/.deps/gflags)  # 设置源码放置的目录

FetchContent_Declare(  # 用于声明依赖包
  gflags  # 依赖包的名字
  GIT_REPOSITORY https://gitee.com/mirrors/gflags.git  # git clone 使用的 url
  GIT_TAG 986e8ee  # git commid ID 或者 tag
)
FetchContent_GetProperties(gflags)  # 获取依赖包的各种目录和目标路径，结合 CMake 其它命令完成各种操作

FetchContent_Declare(
  myCompanyIcons
  URL      https://intranet.mycompany.com/assets/iconset_1.12.tar.gz
  URL_HASH MD5=5588a7b18261c20068beabfb4f530b87
)
```

以上仅为声明不会有代码被下载。

执行 FetchContent_Populate 才能使代码能在 configure 阶段被下载。

下载前也可以设置一些变量对子 CMake 项目进行控制：

```cmake
set(gflags_BUILD_TESTING OFF)  # 设置关闭项目的测试

if(NOT gflags_POPULATED)  # 确保只被拉取一次
  FetchContent_Populate(gflags)  # 拉取代码并设置 gflags_POPULATED
  add_subdirectory(  # 添加 subdirectory 后执行 make 时会编译第三方库
    ${gflags_SOURCE_DIR}
    ${gflags_BINARY_DIR}
  )
endif()
```

add_subdirectory 后，编译出的项目 target 都会进入到当前项目的作用域里，使用target_link_libraries即可完成关联。

---

### 第三方库未使用 CMake

使用的第三方库不一定使用了 CMake，或者使用不是现代 CMake。

这些情况下利用 FetchContent_GetProperties 可以拿到依赖库的各种目录，可以结合 CMake 的其他命令完成各种操作。

比如 Eigen 这个只有头文件的库，虽然使用了 CMake，但项目中测试相关的 target 过多，并且难以方便的禁用。

因此在拿到 Eigen 源码路径后可以自己创建一个简单的 target。

```cmake
include(FetchContent)

FetchContent_Declare(
    eigen3
    URL https://gitlab.com/libeigen/eigen/-/archive/3.3.7/eigen-3.3.7.tar.bz2
    URL_MD5 b9e98a200d2455f06db9c661c5610496
)
FetchContent_GetProperties(eigen3)

if (NOT eigen3_POPULATED)
  FetchContent_Populate(eigen3)
endif ()

add_library(eigen INTERFACE)
target_include_directories(eigen INTERFACE ${eigen3_SOURCE_DIR})
```

还有很多使用 make 作为编译工具的项目，拿到源码目录后可以使用 add_custom_command 和 add_custom_target 原地编译，然后创建一个简单的 imported target。

这里以 uWebSockets 为例，这个库本身是只有头文件的，但使用 git submodules 依赖了一个使用 make 的子项目 uSockets。

```cmake
include(FetchContent)

FetchContent_Declare(
    uWebSockets-git
    GIT_REPOSITORY https://github.com/uNetworking/uWebSockets.git
    GIT_TAG v18
)
FetchContent_GetProperties(uWebSockets-git)

if (NOT uWebSockets-git_POPULATED)
  FetchContent_Populate(uWebSockets-git)
endif ()

add_custom_command(  # 创建一个命令用于编译出 uSockets 的静态库，并且创建好头文件目录
    OUTPUT ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a
    COMMAND cp -r src uWebSockets && make
    WORKING_DIRECTORY ${uWebSockets-git_SOURCE_DIR}
    COMMENT "build uSockets"
    VERBATIM
)

# 创建一个自定义 target
# 依赖上面自定义命令的 OUTPUT
# 这样 CMake 还不会编译这个 target，还需要一个真正的 target 依赖此 target
add_custom_target(uSockets DEPENDS ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a)

# 创建一个imported target 依赖上面的自定义 target，从而确保在使用这个 imported target 时，上面的编译命令能被执行
add_library(uWebSockets STATIC IMPORTED)
set_property(TARGET uWebSockets PROPERTY IMPORTED_LOCATION ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a)
set_target_properties(
    uWebSockets PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${uWebSockets-git_SOURCE_DIR};${uWebSockets-git_SOURCE_DIR}/uSockets/src"
)

add_dependencies(uWebSockets uSockets)
```

总之当拿到源码目录后，可以结合 CMake 的其他命令完成各种操作，毕竟需要的可以只有头文件和链接库文件。

---

### 第三方库使用 CMake，在项目外部构建

CMake Library 项目在 install package 时应该提供文件 "\*-config.cmake" 或 "\*Config.cmake"，其中包含项目相关的 imported target，或者设置链接库路径等变量。

[官方教程](https://cmake.org/cmake/help/latest/guide/tutorial/Adding%20Export%20Configuration.html)

[Tutorial: Easily supporting CMake install and find_package()](https://www.foonathan.net/2016/03/cmake-install/)

这种情况下可以使用find_package命令来寻找依赖。

假设库的名称为 A，调用 find_package(A 1.0) 时，CMake 会尝试在默认路径下寻找  A-config.cmake  或者  AConfig.cmake 。

这个文件可以放在以  A*  为前缀的文件夹下来支持多版本并存。

当然，这个第三方库不一定就安装在默认路径，那么用户可以设置 A_DIR 这个变量，用于提示 CMake 应该去哪里寻找 config 文件。

在找到该文件后， A_FOUND 变量会被设置，同时 config 文件中包含的 target 以及 CMake 变量都会存在于 fink_packge 之后的的作用域里，可以按需使用。

 find_package 有 MODULE 和 CONFIG 两种模式，MODULE模式寻找 Findxxx.cmake 文件，CONFIG模式寻找 config 文件。
没有指定模式时，CMake 优先按 MODULE 模式寻找库，没找到的话再到 CONFIG 模式寻找。

两者一个重要的区别在于，config 脚本由库的开发者提供，find 脚本由使用者提供。

---

### 第三方库未使用 CMake，在项目外部构建

现实并不总是那么美好，第三方库安装时可能没有提供 config 文件，比如使用make作为构建工具的项目。

可以直接使用 find_path 和 find_library 两个命令来寻找头文件以及链接库所在的路径，CMake 会尝试到默认路径下寻找。

同样的，库不一定被安装在默认路径下，这时我们可以指定搜索路径。

找到头文件以及链接库后，可以直接用，也可以创建个 imported target 使用。

```cmake
find_path(
  TENSORRT_INCLUDE_DIR
  NvInfer.h
  HINTS ${TENSORRT_ROOT} ${CUDA_TOOLKIT_ROOT_DIR}
  PATH_SUFFIXES include
)

find_library(
  TENSORRT_INFER_LIB
  libnvinfer.so
  HINTS ${TENSORRT_ROOT} ${CUDA_TOOLKIT_ROOT_DIR}
  PATH_SUFFIXES lib lib64 lib/x64
)

add_library(TENSORRT_IMPORTED STATIC IMPORTED)
set_property(
  TARGET TENSORRT_IMPORTED
  PROPERTY IMPORTED_LOCATION ${TENSORRT_INFER_LIB}
)
set_target_properties(
    TENSORRT_IMPORTED
    PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${TENSORRT_INCLUDE_DIR}
)
```

更优雅的方式是向 CMake 提供一个脚本(Findxxx.cmake)，其中可以使用各种方法(find_path/find_library)找到可能的依赖项，并导出库的信息。

很多基于 make 构建工具的第三方库都可以在网上可以找到 find 脚本，同时 CMake 官方也为我们写好了很多常用库的 Find 脚本，比如：OpenGL、JPEG、ZLIB，对于这些库无需编写 find 脚本直接使用find_package就可以了。

寻找 find 脚本时，CMake 会优先到 CMAKE_MODULE_PATH 变量对应的路径找，随后是 CMake 自带的 find 脚本目录。

如果我们准备好了某个库的 find 脚本，可以把其所在的目录加到CMAKE_MODULE_PATH里，这样find_package就能找到它了。

```cmake
list(APPEND CMAKE_MODULE_PATH "./cmake/")
find_package(MyLib)
if (MyLib_FOUND)
  ......
endif()
```

---

## 创建下游友好的 CMake 项目

- 使用基于 target 的现代 CMake
- 作为库的开发者，在预编译的 package 里提供 config 脚本
- 代码仓库里不要放太多代码无关的大文件，避免下载时间过长
- 打好版本 tag，方便控制版本

---

## find_package 基本调用形式和模块模式

基本调用形式：

```cmake
find_package(<PackageName> [version] [EXACT] [QUIET] [MODULE]
             [REQUIRED] [[COMPONENTS] [components...]]
             [OPTIONAL_COMPONENTS components...]
             [NO_POLICY_SCOPE])
```

查找是否成功可以通过 \<PackageName\>_FOUND 变量的值得到。

成功查找到包后，我们就可以导入包中的目标。

使用 QUIET 选项可以不显示查询过程中产生的信息，比如如果没有使用 REQUIRED 选项，即使包没有被找到，也不会显示任何信息。使用 REQUIRED 选项后，如果包没有被找到，就会产生一个错误信息，中断处理。

对于必须的包组件可以在 COMPONENTS 选项后列出(如果使用了 REQUIRED 选项，也可以在 REQUIRED 选项后列出)。

对于可选的包组件可以在 OPTIONAL_COMPONENTS 选项后列出。包自身定义了可用的包组件和包被认为找到的条件。

version 选项用于指定要查找的包的版本，版本格式为 `major[.minor[.patch[.tweak]]]` 。

使用 EXACT 选项后，查找的包的版本需要和指定的版本准确匹配。如果没有使用 version 选项，没有给出需要的包组件列表，就会使用外部调用的相应参数(包括 version 的 EXACT 选项)。

find_package指令有两种查找包的模式：一种是模块(Module)模式，一种是配置(Config)模式。

默认情况下，首先使用模块(Module)模式，如果没有找到对应的模块(Module)，就会使用配置(Config)模式。

如果使用了 MODULE 选项，使用模块模式失败后，不会继续使用配置(Config)模式。

模块(Module)模式下，CMake会搜索一个名为 Find\<PackageName\>.cmake 的文件。首先使用 CMAKE_MODULE_PATH中 的路径搜索，然后搜索 CMake 自带的 Find 模块。

找到 Find\<PackageName\>.cmake 后，CMake 会读取处理这个文件。这个文件包含了要查找的包的信息。

---

## find_package 完整形式

一般而言，用户只需要使用 find_package 指令的基本调用形式即可。find_package 指令的完整调用形式主要是用来对搜索过程进行更加准确详细的设置。

完整调用形式：

```cmake
find_package(<PackageName> [version] [EXACT] [QUIET]
             [REQUIRED] [[COMPONENTS] [components...]]
             [CONFIG|NO_MODULE]
             [NO_POLICY_SCOPE]
             [NAMES name1 [name2 ...]]
             [CONFIGS config1 [config2 ...]]
             [HINTS path1 [path2 ... ]]
             [PATHS path1 [path2 ... ]]
             [PATH_SUFFIXES suffix1 [suffix2 ...]]
             [NO_DEFAULT_PATH]
             [NO_PACKAGE_ROOT_PATH]
             [NO_CMAKE_PATH]
             [NO_CMAKE_ENVIRONMENT_PATH]
             [NO_SYSTEM_ENVIRONMENT_PATH]
             [NO_CMAKE_PACKAGE_REGISTRY]
             [NO_CMAKE_BUILDS_PATH] # Deprecated; does nothing.
             [NO_CMAKE_SYSTEM_PATH]
             [NO_CMAKE_SYSTEM_PACKAGE_REGISTRY]
             [CMAKE_FIND_ROOT_PATH_BOTH |
              ONLY_CMAKE_FIND_ROOT_PATH |
              NO_CMAKE_FIND_ROOT_PATH])
```

使用 CONFIG 选项或它的同义词 NO_MODULE 选项，以及其它没有在 find_package 的基本调用形式中出现的选项都会强制 find_package 指令直接使用配置(Config)模式，跳过模块模式处理。

配置模式下，CMake会搜索一个由包提供的配置文件。

指令返回后，\<PackageName\>_DIR 变量保存了包含这一配置文件的目录名。

默认情况下，find_package 指令使用 \<PackageName\> 作为包名进行搜索。

如果使用了 NAMES 选项，就会使用 NAMES 选项后的列表中的名称作为包名搜索 \<PackageName\>Config.cmake 或 \<package-name\>-config.cmake 。

可以使用 CONFIGS 选项替换使用的配置文件名称。找到配置文件后，配置文件会被 CMake 读取并执行。配置文件是由包自身提供，所以具备了包的所有信息。配置文件的完整路径会被存储在 \<PackageName\>_CONFIG 变量中。

在搜索过程中所有被查找过的可能被使用的包的配置文件会被存储在 \<PackageName\>_CONSIDERED_CONFIGS 变量中，包的版本会被存储在 \<PackageName\>_CONSIDERED_VERSIONS 变量中。

如果最后没有找到包的配置文件，并且没有使用 QUIET 选项，那么 CMake 就会产生一条描述这一错误的信息。

如果使用了 REQUIRED 选项，包没有被找到就会导致 CMake 产生一个致命错误，中断处理。如果 \<PackageName\>_DIR 变量已经被设置为一个不包含包配置文件的目录，CMake会忽略掉它，重新开始搜索。

---

## find_package 版本选择

配置模式下，如果使用了 version 选项，将会搜索与指定的版本相匹配的包(版本格式为 `major[.minor[.patch[.tweak]]]` )。

如果使用了 EXACT 选项，则会搜索与指定版本准确匹配的包。

CMake 没有对版本号有任何约定。包的版本号由包自身提供的版本文件得到。对于一个候选的包配置文件 \<config-file\>.cmake ，它对应的版本文件 \<config-file\>-version.cmake 或 \<config-file\>Version.cmake 在同一目录下。

如果对应的版本文件不存在，包配置文件假设包与任何指定的版本都不匹配。可以使用 CMakePackageConfigHelpers 模块创建一个最基本的版本文件。版本文件找到后，会被读取用来检测包的版本号。

版本文件会被在一个包含了以下变量的内部作用域下读取执行：

```text
PACKAGE_FIND_NAME：<PackageName>
PACKAGE_FIND_VERSION：完整的版本字符串
PACKAGE_FIND_VERSION_MAJOR：主版本号，如果没有指定则为0
PACKAGE_FIND_VERSION_MINOR：次版本号，如果没有指定则为0
PACKAGE_FIND_VERSION_PATCH：补丁版本号，如果没有指定则为0
PACKAGE_FIND_VERSION_TWEAK：调整版本号，如果没有指定则为0
PACKAGE_FIND_VERSION_COUNT：指定的版本号数量，值为0到4
```

版本文件使用上面这些变量检测包版本是否满足，然后设置下面这些变量的值：

```text
PACKAGE_VERSION：包的完整版本字符串
PACKAGE_VERSION_EXACT：如果包版本准确匹配，这一变量被设置为true
PACKAGE_VERSION_COMPATIBLE：如果包版本匹配，这一变量被设置为true
PACKAGE_VERSION_UNSUITABLE：如果包不匹配任何版本，这一变量被设置为true
```

上面这些变量会被 find_package 指令检测用来确定包的版本是否可以被接收。它们在 find_package 指令返回后不可用。如果包的版本可以被接收，下面这些变量会被设置：

```text
<PackageName>_VERSION：包的完整版本字符串
<PackageName>_VERSION_MAJOR：主版本号，如果没有提供则为0
<PackageName>_VERSION_MINOR：次版本号，如果没有提供则为0
<PackageName>_VERSION_PATCH：补丁版本号，如果没有提供则为0
<PackageName>_VERSION_TWEAK：调整版本号，如果没有提供则为0
<PackageName>_VERSION_COUNT：使用的版本号数量，值为0到4
```

当多个包配置文件匹配指定的包版本时，如果没有设置 CMAKE_FIND_PACKAGE_SORT_ORDER 变量进行排序，哪一个包被选定是未定义的。

使用 CMAKE_FIND_PACKAGE_SORT_ORDER 和 CMAKE_FIND_PACKAGE_SORT_DIRECTION 变量可以控制 find_package 指令的检测顺序。

比如，为了选择版本最高的包，可以在调用 find_package 指令前按照下面这样设置：

```cmake
SET(CMAKE_FIND_PACKAGE_SORT_ORDER NATURAL)
SET(CMAKE_FIND_PACKAGE_SORT_DIRECTION DEC)
```

---

## find_package 搜索过程

CMake包含了一组前缀用于查找安装的包。这一组前缀之后连接了多个用于查找配置文件的目录。

下面列出了这些目录。W代表在Windows下使用，U代表在UNIX下使用，A代表在Apple下使用的目录：

```text
<prefix>/                                                       (W)
<prefix>/(cmake|CMake)/                                         (W)
<prefix>/<name>*/                                               (W)
<prefix>/<name>*/(cmake|CMake)/                                 (W)
<prefix>/(lib/<arch>|lib*|share)/cmake/<name>*/                 (U)
<prefix>/(lib/<arch>|lib*|share)/<name>*/                       (U)
<prefix>/(lib/<arch>|lib*|share)/<name>*/(cmake|CMake)/         (U)
<prefix>/<name>*/(lib/<arch>|lib*|share)/cmake/<name>*/         (W/U)
<prefix>/<name>*/(lib/<arch>|lib*|share)/<name>*/               (W/U)
<prefix>/<name>*/(lib/<arch>|lib*|share)/<name>*/(cmake|CMake)/ (W/U)

对于macOS系统，会使用下面的目录搜索配置文件：
<prefix>/<name>.framework/Resources/                    (A)
<prefix>/<name>.framework/Resources/CMake/              (A)
<prefix>/<name>.framework/Versions/*/Resources/         (A)
<prefix>/<name>.framework/Versions/*/Resources/CMake/   (A)
<prefix>/<name>.app/Contents/Resources/                 (A)
<prefix>/<name>.app/Contents/Resources/CMake/           (A)
```

上面目录中的 \<name\> (大小写无关)对应我们在 find_package 指令中指定的名称(\<PackageName\>和使用 NAMES 选项指定的名称)。

如果设置了CMAKE_LIBRARY_ARCHITECTURE变量，就会启用 lib/\<arch\> 这一搜索路径。

lib* 包含了类似 lib64、lib32、libx32、lib 这些值，也是按这一顺序进行搜索的：

- 如果 FIND_LIBRARY_USE_LIB64_PATHS 变量被设置为 TRUE，会启用 lib64 搜索路径。
- 如果 FIND_LIBRARY_USE_LIB32_PATHS 变量被设置为 TRUE，会启用 lib32 搜索路径。
- 如果 FIND_LIBRARY_USE_LIBX32_PATHS 变量被设置为 TRUE，会启用 libx32 搜索路径。

lib 搜索路径总是被使用。

如果使用了 PATH_SUFFIXES 选项，指定的后缀会被加到 W 和 U 类型目录名称之后。

这一组目录主要用于和在安装树中提供了配置文件的程序进行协作。

上面**标记了 W 的目录主要用于 Windows 系统**，通常是程序的安装目录。

标记了 **U 的目录主要用于 UNIX 系统**，通常是系统的默认包目录。

但实际上标记了 W 和 U 的目录会被所有平台搜索。标记了 A 的目录主要用于苹果平台。

CMAKE_FIND_FRAMEWORK 和 CMAKE_FIND_APPBUNDLE 变量决定了苹果平台的搜索顺序。

这一组目录的前缀由下面得到。

如果使用了 NO_DEFAULT_PATH 选项，那么所有 NO_* 选项都会被启用。

1. CMake 变量 \<PackageName\>_ROOT 和环境变量 \<PackageName\>_ROOT`中指定的搜索路径。
包的root路径变量以栈的形式维护，如果没有使用 NO_PACKAGE_ROOT_PATH 选项，在一个搜索模块中调用 find_package 指令，来自搜索模块的root路径也会被搜索。
2. CMake 隐含变量指定的搜索路径。主要是在命令行上使用 **-DVAR=value** 来指定。指定的搜索路径以分号分割。可以使用 NO_CMAKE_PATH 选项跳过这一搜索路径：
    - CMAKE_PREFIX_PATH
    - CMAKE_FRAMEWORK_PATH
    - CMAKE_APPBUNDLE_PATH
3. CMake 环境变量指定的搜索路径。主要来自用户的终端配置，指定的搜索路径以原生的路径分隔进行分割。可以使用NO_CMAKE_ENVIRONMENT_PATH跳过这一搜索路径：
    - \<PackageName\>\_DIR
    - CMAKE_PREFIX_PATH
    - CMAKE_FRAMEWORK_PATH
    - CMAKE_APPBUNDLE_PATH
4. 使用 HINTS 选项指定的搜索路径。这一搜索路径通常由计算得到。比如使用一个已有信息作为搜索路径的提示。如果要使用硬编码的路径信息应该使用 PATHS 选项。
5. 系统的环境变量指定的搜索路径。使用 NO_SYSTEM_ENVIRONMENT_PATH 选项可以跳过这一搜索路径。路径中以 /bin 和 /sbin 结尾的目录会被自动转换为它们的父目录：PATH。
6. CMake 的用户包注册表指定的搜索路径。使用 NO_CMAKE_PACKAGE_REGISTRY 选项或设置 CMAKE_FIND_PACKAGE_NO_PACKAGE_REGISTRY 变量为 TRUE 可以跳过这一搜索路径。
7. 当前系统平台的 CMake 变量指定的搜索路径。使用 NO_CMAKE_SYSTEM_PATH 可以跳过这一搜索路径：
    - CMAKE_SYSTEM_PREFIX_PATH
    - CMAKE_SYSTEM_FRAMEWORK_PATH
    - CMAKE_SYSTEM_APPBUNDLE_PATH
8. CMake 的系统包注册表指定的搜索路径。使用 NO_CMAKE_SYSTEM_PACKAGE_REGISTRY 选项或设置 CMAKE_FIND_PACKAGE_NO_SYSTEM_PACKAGE_REGISTRY 变量为 TRUE 可以跳过这一搜索路径。
9. 使用 PATHS 选项指定的搜索路径。也就是硬编码的搜索路径。

使用 CMake 变量 CMAKE_FIND_ROOT_PATH 可以指定一个或多个目录作为其它搜索路径的前缀，使用它我们可以重定位整个搜索路径。

使用 CMAKE_STAGING_PREFIX 变量衍生的搜索路径是一个系统平台特定路径，不会被重定位。默认情况下，CMAKE_FIND_ROOT_PATH 变量的值为空。

使用 CMAKE_SYSROOT 变量也可以指定一个搜索路径前缀。

这些变量对于跨平台编译有很大的作用。

默认情况下，CMake 首先使用 CMAKE_FIND_ROOT_PATH 中的路径，然后使用 CMAKE_SYSROOT 中的路径，接着使用非根路径。

可以通过设置 CMAKE_FIND_ROOT_PATH_MODE_PACKAGE 变量调整这一默认搜索方式。可以使用下面这些选项针对单个 find_package 指令进行控制：

- CMAKE_FIND_ROOT_PATH_BOTH：按照上面的描述进行搜索。
- NO_CMAKE_FIND_ROOT_PATH：不使用CMAKE_FIND_ROOT_PATH变量进行搜索。
- ONLY_CMAKE_FIND_ROOT_PATH：只搜索重定位的目录和 CMAKE_STAGING_PREFIX变量衍生的目录。

默认情况下的搜索顺序满足大多数使用场景。一般情况下，我们只需要使用 NO_* 类选项多次调用 find_package 指令就可以按照我们的要求来搜索包：

```cmake
find_package (<PackageName> PATHS paths... NO_DEFAULT_PATH)
find_package (<PackageName>)
```

上面的两次 find_package 指令只要有一次成功找到了包，就不会再次搜索这个包。

默认情况下，存储在结果变量中的值是最后找到的文件路径。

在使用 find_package 指令前设置 CMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS 变量的值为 TRUE，可以将找到的符号链接解析为真实的文件路径。

通过设置 CMAKE_DISABLE_FIND_PACKAGE_\<PackageName\> 变量为 TRUE 可以让非 REQUIRED 的 find_package 指令调用失效。

---

## find_package 包文件接口变量

载入一个搜索模块或包配置文件时，find_package 指令设置了一些有关调用参数的变量(find_package 指令调用返回后，这些变量会被还原)：

```text
CMAKE_FIND_PACKAGE_NAME：搜索的包名
<PackageName>_FIND_REQUIRED：如果使用了REQUIRED选项，值为TRUE
<PackageName>_FIND_QUIETLY：如果使用了QUIET选项，值为TRUE
<PackageName>_FIND_VERSION：搜索的包的完整版本字符串
<PackageName>_FIND_VERSION_MAJOR：包的主版本号，如果没有指定则为0
<PackageName>_FIND_VERSION_MINOR：包的次版本号，如果没有指定则为0
<PackageName>_FIND_VERSION_PATCH：包的补丁版本号，如果没有指定则为0
<PackageName>_FIND_VERSION_TWEAK：包的调整版本号，如果没有指定则为0
<PackageName>_FIND_VERSION_COUNT：包使用的版本数量，值为0到4
<PackageName>_FIND_VERSION_EXACT：如果使用了EXACT选项，值为TRUE
<PackageName>_FIND_COMPONENTS：搜索的包的组件
<PackageName>_FIND_REQUIRED_<c>：如果组件<c>被指定为必须，值为TRUE。如果组件<c>是可选的，值为FALSE
```

在模块(Module)模式下，被加载的搜索模块负责查找满足这些信息的包。

在配置(Config)模式下，find_package 指令会自动处理 REQUIRED、QUIET、version 选项，然后让包配置文件自己处理组件需求。

包配置文件通过设置 \<PackageName\>_FOUND 变量为 FALSE 表明组件需求不能够被满足。

```cmake
  if (MINGW)
    option(WITH_GFLAGS "build with GFlags" OFF)
  else()
    option(WITH_GFLAGS "build with GFlags" ON)
  endif()

  set(GFLAGS_LIB)

  if (WITH_GFLAGS)
    set(gflags_DIR /home/gitSrc/google/gflags-2.2.0/build)

    # Config with namespace available since gflags 2.2.2
    option(GFLAGS_USE_TARGET_NAMESPACE "Use gflags import target with namespace." ON)

    find_package(
      gflags CONFIG
      PATHS /home/gitSrc/google/gflags-2.2.0
      NO_DEFAULT_PATH
    )

    if(gflags_FOUND)
      if(TARGET ${GFLAGS_TARGET})
        # Config with GFLAGS_TARGET available since gflags 2.2.0
        set(GFLAGS_LIB ${GFLAGS_TARGET})
      else()
        # Config with GFLAGS_LIBRARIES available since gflags 2.1.0
        set(GFLAGS_LIB ${gflags_LIBRARIES})
      endif()
    else()
      find_package(
        gflags REQUIRED
        PATHS /home/gitSrc/google/gflags-2.2.0
        NO_DEFAULT_PATH
      )
      set(GFLAGS_LIB gflags::gflags)
    endif()

    include_directories(${GFLAGS_INCLUDE_DIR})
    list(APPEND THIRDPARTY_LIBS ${GFLAGS_LIB})

    add_definitions(-DGFLAGS=1)

    message("gflags_CONFIG: ${gflags_CONFIG}")
    message("GFLAGS_CONFIG: ${gflags_CONFIG}")
    message("gflags_INCLUDE_DIR: ${GFLAGS_INCLUDE_DIR}")
    message("GFLAGS_LIB: ${GFLAGS_LIB}")
  endif()

#CMake Error at CMakeLists.txt:142 (find_package):
#  Could not find a package configuration file provided by "gflags" with any
#  of the following names:
#
#    gflagsConfig.cmake
#    gflags-config.cmake
#
#  Add the installation prefix of "gflags" to CMAKE_PREFIX_PATH or set
#  "gflags_DIR" to a directory containing one of the above files.  If "gflags"
#  provides a separate development package or SDK, be sure it has been
#  installed.

set(gflags_DIR /home/gitSrc/google/gflags-2.2.0/build)  # 有 gflagsconfig.cmake 的直接路径

find_package(
  gflags REQUIRED
  PATHS /home/gitSrc/google/gflags-2.2.0
  COMPONENTS static   #使用静态库
  NO_DEFAULT_PATH
)
```

---

## 使用GoogleTest库进行单元测试

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-014 LANGUAGES CXX)

option(ENABLE_UNIT_TESTS "Enable unit test" ON)  # 定义option，是否开启单元测试，默认打开
message(STATUS "Enable testing: ${ENABLE_UNIT_TESTS}")

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_library(sum_integers sum.cc)  # 将sum.cc编译成库

add_executable(main main.cc)  # 添加可执行程序
target_link_libraries(main sum_integers)

if (ENABLE_UNIT_TESTS)

include(FetchContent)  # cmake include FetchContent

FetchContent_Declare(  # 声明依赖相关信息
    googletest
    GIT_REPOSITORY https://gitee.com/mirrors/googletest.git
    GIT_TAG release-1.11.0
)
FetchContent_GetProperties(googletest)  # 获取各种属性

if (NOT googletest_POPULATED)  # 确保相关依赖只被下载了1次
  FetchContent_Populate(googletest)  # 下载相关依赖，执行完成后会设置googletest_POPULATED
  # Prevent GoogleTest from overriding our compiler/linker options
  # when building with Visual Studio
  set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
  # Prevent GoogleTest from using PThreads
  set(gtest_disable_pthreads ON CACHE BOOL "" FORCE)
  # adds the targers: gtest, gtest_main, gmock, gmock_main
  add_subdirectory(
    ${googletest_SOURCE_DIR}
    ${googletest_BINARY_DIR}
    )
  # Silence std::tr1 warning on MSVC
  if (MSVC)
    foreach(_tgt gtest gtest_main gmock gmock_main)
      target_compile_definitions(${_tgt}
        PRIVATE
            "_SILENCE_TR1_NAMESPACE_DEPRECATION_WARNING"
      )
    endforeach()
  endif()
endif()

add_executable(sum_ut)  # 添加单元测试执行程序
target_sources(sum_ut
  PRIVATE
      sum_ut.cc)
target_link_libraries(sum_ut
  PRIVATE
    sum_integers
    gtest_main
  )

enable_testing()  # cmake ut enable

add_test(  # add ut
    NAME sum_ut
    COMMAND $<TARGET_FILE:sum_ut>
)

endif()
```

---

## 使用动态分析来检测内存缺陷

在使用CMake/CTest测试时使用Valgrind对内存问题进行警告：

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(example-015 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_library(test_v test.cc)

add_executable(main)
target_sources(main
  PRIVATE
    test.cc
    main.cc
)
target_link_libraries(main
  PRIVATE
    test_v
)

find_program(MEMORYCHECK_COMMAND NAMES valgrind)  # 查找valgrind程序，可通过 sudo apt-get install valgrind 安装
set(MEMORYCHECK_COMMAND_OPTIONS "--trace-children=yes --leak-check=full")  # 设置valgrind执行参数

include(CTest)  # 添加Cmake/CTest

enable_testing()

add_test(
    NAME main
    COMMAND $<TARGET_FILE:main>
)

# 编译完成后执行在build目录下：
#  1.执行ctest会进行所有测试例的单元测试
#  2.执行ctest -R main会进行main测试例的单元测试
#  3.ctest -T memcheck会进行内存缺陷检查
# 一些工具，如代码覆盖率和静态分析工具，可以进行类似地设置。然而，其中一些工具的使用更加复杂，因为需要专门的构建和工具链。

# 每一个测试例都可以进行属性设置：
#  1.set_tests_properties(main PROPERTIES TIMEOUT 10)，设置超时时间，超时后测试将被中止，测试例标记为失败。
#  2.set_tests_properties(main PROPERTIES WILL_FAIL true)
#    正常情况下返回非零值被认为测试例测试失败，如果WILL_FAIL设置为true，返回非零值认为是成功。

# 当存在多个测试例时，使用ctest --parallel core_num，可以开启并行测试，core_num为CPU核心数。
#  1.set_tests_properties(main1 main2 PROPERTIES COST 0.5)，批量设置多个测试例每次测试的时间成本。
#  2.set_tests_properties(main1 main2 PROPERTIES LABELS "label1")
#    为多个测试例添加标签，执行ctest -R label1，会对所有标签为label1的测试例进行测试。
```
